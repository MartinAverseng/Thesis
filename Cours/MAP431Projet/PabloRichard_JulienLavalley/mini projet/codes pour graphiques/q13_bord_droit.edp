//Constants
// Dimension constants

real L            = 1;
real l            = 1;

// Discretization constants
int Nx             = 20;
int Ny             = 20;
int steps          = 150;      // nombre d'étapes temporelles
real dt            = 0.1;     // intervalle de temps entre chaque étape

// Triangular Mesh with references
int dirichlet = 1;
int neumann = 0;
int[int] labs = [dirichlet,neumann,dirichlet,dirichlet];
mesh Th = square(Nx,Ny,[L*x,l*y],label=labs);

// FE Spaces and functions
fespace Vh(Th,P2);
Vh u1,u2,v1,v2;

fespace Ph(Th,P1);
Ph p,q;


// Variationnal formulations

//FV2
problem LaplaceAbsorbDirichlet2([u1,u2,p],[v1,v2,q])
= int2d(Th)(2*dx(u1)*dx(v1) + 2*dy(u2)*dy(v2) + dy(u1)*dy(v1)
+ dx(u2)*dx(v2) + dy(u1)*dx(v2) + dx(u2)*dy(v1))
- int2d(Th)(p*(dx(v1)+dy(v2)))
- int2d(Th)(q*(dx(u1)+dy(u2)))
- int2d(Th)(-v2)
+ on(dirichlet, u1=0, u2=0);


LaplaceAbsorbDirichlet2;

Th = movemesh(Th,[x,y]+dt*[u1,u2]);
Th = adaptmesh(Th,u1^2+u2^2);
plot(Th, wait=1, cmm="Debut d'ecoulement avec bord droit libre",ps="q13_bord_droit_deb.eps");

for (int i=1;i<=steps;i++) // début de la boucle sur les maillages
{
LaplaceAbsorbDirichlet2;

Th = movemesh(Th,[x,y]+dt*[u1,u2]);
Th = adaptmesh(Th,u1^2+u2^2);
plot(Th, wait=0, cmm="Mesh");
cout << "Step " << i << " done"<<endl;
}
plot(Th, wait=1, cmm="Ecoulement par le bord droit apres 15 secondes",ps="q13_bord_droit_fin.eps");
