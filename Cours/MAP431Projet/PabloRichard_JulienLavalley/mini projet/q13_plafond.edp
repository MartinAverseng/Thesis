// Constants
// Dimension constants
real L             = 1;       // largeur du plafond
real l             = 0.2;     // epaisseur initiale du fluide
real h             = 0.1;     // largeur du trou

// Discretization constants
int steps          = 250;     // nombre d'etapes temporelles
real dt            = 0.2;     // intervalle de temps entre chaque etape

// Triangular Mesh with references
int dirichlet = 1;
int neumann = 0;
border top(t=L,0){x=t;y=l;label = neumann;};
border left(t=l,0){x=0;y=t;label = dirichlet;};
border right(t=0,l){x=L;y=t;label = dirichlet;};
border hole(t=(L-h)/2,(L+h)/2){x=t;y=0;label = neumann;};
border groundLeft(t=0,(L-h)/2){x=t;y=0;label = dirichlet;};
border groundRight(t=(L+h)/2,1){x=t;y=0;label = dirichlet;};
mesh Th = buildmesh (top(6) + right(4) + left(4)
+ hole(5) + groundLeft(4) + groundRight(4));

// FE Spaces and functions
fespace Vh(Th,P2);
Vh u1,u2,v1,v2;
fespace Ph(Th,P1);
Ph p,q;

// Variationnal formulation - FV2
problem LaplaceAbsorbDirichlet2([u1,u2,p],[v1,v2,q])
= int2d(Th)(2*dx(u1)*dx(v1) + 2*dy(u2)*dy(v2) + dy(u1)*dy(v1)
+ dx(u2)*dx(v2) + dy(u1)*dx(v2) + dx(u2)*dy(v1))
- int2d(Th)(p*(dx(v1)+dy(v2)))
- int2d(Th)(q*(dx(u1)+dy(u2)))
- int2d(Th)(-v2)
+ on(dirichlet, u1=0, u2=0);

for (int i=1;i<=steps;i++){ // debut de la boucle sur les maillages
LaplaceAbsorbDirichlet2;
Th = movemesh(Th,[x,y]+dt*[u1,u2]);
Th = adaptmesh(Th,u1^2+u2^2);
plot(Th, wait=0, cmm="Ecoulement par un trou dans le plafond ...");
cout << "Step " << i << " done"<<endl;}
