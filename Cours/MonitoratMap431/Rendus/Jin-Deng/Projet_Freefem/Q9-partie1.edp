//definir le maillage
int N=20;
border a1(t=0,1){x=t; y=0; label=1;}
border a2(t=0,1){x=1; y=t; label=2;}
border a3(t=0,1){x=(1-t); y=1; label=3;}
border a4(t=0,1){x=0; y=(1-t); label=4;}
mesh Th = buildmesh(a1(N)+a2(N)+ a3(N)+a4(N));
//plot(Th, wait=1, cmm="Maillage, N=20");

//definition de f=[fx,fy]
func fx=cos(y);
func fy=sin(x);

////////P1 pour u et P0 pour p
fespace Vh1(Th,P1);
fespace Qh1(Th,P0);

Vh1 uhx, uhy, vhx, vhy; 
Qh1 p,q;

problem question901FV1([uhx, uhy, p], [vhx, vhy, q])=
  int2d(Th)(dx(uhx)*dx(vhx)+dy(uhx)*dy(vhx)+dx(uhy)*dx(vhy)+dy(uhy)*dy(vhy)-p*(dx(vhx)+dy(vhy))-q*(dx(uhx)+dy(uhy)))
  -int2d(Th)(fx*vhx+fy*vhy)
  +on(1,2,3,4,uhx=fx,uhy=fy);
  
question901FV1;
plot([uhx,uhy],wait=1,fill=1,dim=2,value=10,cmm="La solution de FV1-901");
plot(p,wait=1,fill=1,dim=2,value=10,cmm="La solution de FV1-901");

problem question901FV2([uhx, uhy, p], [vhx, vhy, q])=
  int2d(Th)(1/2*(4*dx(vhx)*dx(uhx)+(dy(vhx)+dx(vhy))*(dy(uhx)+dx(uhy))*2+4*dy(vhy)*dy(uhy))-p*(dx(vhx)+dy(vhy))-q*(dx(uhx)+dy(uhy)))
  -int2d(Th)(fx*vhx+fy*vhy)
  +on(1,2,3,4,uhx=fx,uhy=fy);

question901FV2;
plot([uhx,uhy],wait=1,fill=1,dim=2,value=10,cmm="La solution de FV2-901");
plot(p,wait=1,fill=1,dim=2,value=10,cmm="La solution de FV2-901");
